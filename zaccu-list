# $1 - main window name
# $2, $3 - width and height of the window
# $4 - optional status window name
# $5, $6 - width and height of the window
#
# $ZACCULIST_NONSELECTABLE_ELEMENTS - array of indexes (1-based) that cannot be selected
# $REPLY is the output variable - contains index (1-based) or -1 when no selection
# $reply (array) is the second part of the output - use the index (REPLY) to get selected element
#
# This function outputs a list of elements that can be navigated with keyboard.
# Besides vertical navigation, it does horizontal navigation over elements of line.
# Uses curses library

emulate -LR zsh

setopt typesetsilent extendedglob noshortloops

_zacculist_has_terminfo=0

zmodload zsh/curses
zmodload zsh/terminfo 2>/dev/null && _zacculist_has_terminfo=1

trap "REPLY=-2; reply=(); return" TERM INT QUIT

# Drawing and input
autoload zaccu-list-draw zaccu-list-input zaccu-process-buffer

# Outputs a message in the bottom of the screen
_zacculist_status_msg() {
    # -1 for border, -1 for 0-based indexing
    zcurses move "$wname_status" 1 2
    zcurses clear "$wname_status" eol
    zcurses string "$wname_status" "$1"
    #status_msg_strlen is localized in caller
    status_msg_strlen=$#1

    if [ -n "$2" ]; then
        local cmd=" Cmd: " wd=" Working dir: "
        zcurses string "$wname_status" "$cmd"
        zcurses attr "$wname_status" "red/$background"
        zcurses string "$wname_status" "$2"
        zcurses attr "$wname_status" "$colorpair"
        zcurses string "$wname_status" "$wd"
        zcurses attr "$wname_status" "green/$background"
        zcurses string "$wname_status" "$3"
        zcurses attr "$wname_status" "$colorpair"
        (( status_msg_strlen += ${#cmd} + ${#2} + ${#wd} + ${#3} ))
    fi
}

# $1 is window name, $2 is the expected cursor state (0 invisible, 1 visible)
# Prefer tput, then module terminfo
_zacculist_cursor_visibility() {
    # If $1 = plain, then just output the
    # codes without any state management
    if [ "$1" != "plain" ]; then
        # Don't change already set cursor state
        [ "$cursor_state[$1]" = "$2" ] && return
        cursor_state[$1]="$2"
    fi

    if type tput 2>/dev/null 1>&2; then
        [ "$2" = "1" ] && { tput cvvis; tput cnorm }
        [ "$2" = "0" ] && tput civis
    elif [ "$_zacculist_has_terminfo" = "1" ]; then
        [ "$2" = "1" ] && { [ -n $terminfo[cvvis] ] && echo -n $terminfo[cvvis];
                           [ -n $terminfo[cnorm] ] && echo -n $terminfo[cnorm] }
        [ "$2" = "0" ] && [ -n $terminfo[civis] ] && echo -n $terminfo[civis]
    fi 
}

_zacculist_compute_user_vars_difference() {
        if [[ "${(t)ZACCULIST_NONSELECTABLE_ELEMENTS}" != "array" &&
                "${(t)ZACCULIST_NONSELECTABLE_ELEMENTS}" != "array-local" ]]
        then
            last_element_difference=0
            current_difference=0
        else
            last_element_difference=$#ZACCULIST_NONSELECTABLE_ELEMENTS
            current_difference=0
            local idx
            for idx in "${(n)ZACCULIST_NONSELECTABLE_ELEMENTS[@]}"; do
                [ "$idx" -le "$ZACCULIST_CURRENT_IDX" ] && current_difference+=1 || break
            done
        fi
}

# List was processed, check if variables aren't off range
_zacculist_verify_vars() {
    [ "$ZACCULIST_CURRENT_IDX" -gt "$last_element" ] && ZACCULIST_CURRENT_IDX="$last_element"
    [[ "$ZACCULIST_CURRENT_IDX" -eq 0 && "$last_element" -ne 0 ]] && ZACCULIST_CURRENT_IDX=1
    (( ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN=0+((ZACCULIST_CURRENT_IDX-1)/page_height)*page_height+1 ))
}

# Compute the variables which are shown to the user
_zacculist_setup_user_vars() {
    if [ "$1" = "1" ]; then
        # Basic values when there are no non-selectables
        ZACCULIST_USER_CURRENT_IDX="$ZACCULIST_CURRENT_IDX"
        ZACCULIST_USER_LAST_ELEMENT="$last_element"
    else
        _zacculist_compute_user_vars_difference
        ZACCULIST_USER_CURRENT_IDX=$(( ZACCULIST_CURRENT_IDX - current_difference ))
        ZACCULIST_USER_LAST_ELEMENT=$(( last_element - last_element_difference ))
    fi
}

# Functionality inherited from n-list: ability to colorify
# selected keywords (via ZACCULIST_COLORING_PATTERN). Here
# it uses color mark (\6) inseted of direct usage of ANSI
# color codes
#
_zacculist_colorify_disp_list() {
    local col=$'\6' reset=$'\25'
    [ -n "$ZACCULIST_COLORING_COLOR" ] && col="$ZACCULIST_COLORING_COLOR"
    [ -n "$ZACCULIST_COLORING_END_COLOR" ] && reset="$ZACCULIST_COLORING_END_COLOR"

    if [ "$ZACCULIST_COLORING_MATCH_MULTIPLE" -eq 1 ]; then
        disp_list=( "${(@)disp_list//(#mi)$~ZACCULIST_COLORING_PATTERN/$col${MATCH}$reset}" )
    else
        disp_list=( "${(@)disp_list/(#mi)$~ZACCULIST_COLORING_PATTERN/$col${MATCH}$reset}" )
    fi
}

# Changes color marks (\3, \4, \5, \6, \7) into ANSI color codes (green, yellow, magenta, cyan, red)
_zacculist_translate_color_marks_in_disp_list() {
    local ACC_RESET=$'\e[0m' ACC_RED=$'\e[0;31m' ACC_GREEN=$'\e[0;32m'
    local ACC_YELLOW=$'\e[1;33m' ACC_MAGENTA=$'\e[0;35m' ACC_CYAN=$'\e[0;36m'

    disp_list=( "${disp_list[@]//(#b)$'\3'([^$'\25']#)$'\25'/$ACC_GREEN$match[1]$ACC_RESET}" )
    disp_list=( "${disp_list[@]//(#b)$'\4'([^$'\25']#)$'\25'/$ACC_YELLOW$match[1]$ACC_RESET}" )
    disp_list=( "${disp_list[@]//(#b)$'\5'([^$'\25']#)$'\25'/$ACC_MAGENTA$match[1]$ACC_RESET}" )
    disp_list=( "${disp_list[@]//(#b)$'\6'([^$'\25']#)$'\25'/$ACC_CYAN$match[1]$ACC_RESET}" )
    disp_list=( "${disp_list[@]//(#b)$'\7'([^$'\25']#)$'\25'/$ACC_RED$match[1]$ACC_RESET}" )
}

# Changes color marks (\3, \4, \5, \6, \7) into ANSI color codes (green, yellow, magenta, cyan, red)
#
# $1 - buffer to operate on
# $REPLY - modified buffer
#
_zacculist_translate_color_marks() {
    local ACC_RESET=$'\e[0m' ACC_RED=$'\e[0;31m' ACC_GREEN=$'\e[0;32m'
    local ACC_YELLOW=$'\e[1;33m' ACC_MAGENTA=$'\e[0;35m' ACC_CYAN=$'\e[0;36m'

    local buf="$1"

    buf=( "${buf//(#b)$'\3'([^$'\25']#)$'\25'/$ACC_GREEN$match[1]$ACC_RESET}" )
    buf=( "${buf//(#b)$'\4'([^$'\25']#)$'\25'/$ACC_YELLOW$match[1]$ACC_RESET}" )
    buf=( "${buf//(#b)$'\5'([^$'\25']#)$'\25'/$ACC_MAGENTA$match[1]$ACC_RESET}" )
    buf=( "${buf//(#b)$'\6'([^$'\25']#)$'\25'/$ACC_CYAN$match[1]$ACC_RESET}" )
    buf=( "${buf//(#b)$'\7'([^$'\25']#)$'\25'/$ACC_RED$match[1]$ACC_RESET}" )

    REPLY="$buf"
}

# $1 - buffer to operate on
# $2 - target color mark
# $REPLY - modified buffer
#
_zacculist_replace_color_marks_with() {
    local buf="$1" target_mark="$2"
    buf=${buf//$'\3'/$target_mark}
    buf=${buf//$'\4'/$target_mark}
    buf=${buf//$'\5'/$target_mark}
    buf=${buf//$'\6'/$target_mark}

    # No existing replaced marks – then just add the mark
    if [ "${buf/$target_mark/}" = "$buf" ]; then
        buf="${target_mark}${buf}"$'\25'
    fi

    REPLY="$buf"
}

_zacculist_mark_current_segment_in_current_element() {
        # Check if current element is in ZACCULIST_NONSELECTABLE_ELEMENTS
        # If yes, don't mark current segment
        [[ -z "$ZACCULIST_SEARCH_BUFFER" && "${ZACCULIST_NONSELECTABLE_ELEMENTS[(r)$ZACCULIST_CURRENT_IDX]}" = "$ZACCULIST_CURRENT_IDX" ]] && return

        # There will be no colorifying for current element
        integer ZACCULIST_CURRENT_PAGE_IDX=$(( (ZACCULIST_CURRENT_IDX-1)%page_height+1 ))
        local element="$list[ZACCULIST_CURRENT_IDX]"
        local output=""

        [[ "$ZACCULIST_CURRENT_SEGMENT" -lt 1 ]] && ZACCULIST_CURRENT_SEGMENT=1
        [[ "$ZACCULIST_CURRENT_SEGMENT" -gt "$nseg" ]] && ZACCULIST_CURRENT_SEGMENT="$nseg"
        integer active_segment="$ZACCULIST_CURRENT_SEGMENT"

        # Prepare output variables for zaccu-process-buffer
        local ZACCU_PB_WORDS="" ZACCU_PB_WORDS_BEGINNINGS="" ZACCU_PB_SPACES=""
        local ZACCU_PB_SELECTED_WORD="" ZACCU_PB_LEFT="" ZACCU_PB_RIGHT=""

        # Lets find interesting segment and mark it with bells (\7),
        # that are translated to red color
        zaccu-process-buffer "$element"
        integer size="${#ZACCU_PB_WORDS}" i
        local buf=""
        for (( i=1; i<=size; i++ )); do
            if [[ "$i" -eq "$active_segment" ]]; then
                ZACCU_PB_WORDS[i]=${ZACCU_PB_WORDS[i]//$'\n'/\\n}
                _zacculist_replace_color_marks_with "${ZACCU_PB_WORDS[i]}" $'\7'
                ZACCU_PB_WORDS[i]="$REPLY"
            fi
            buf+="$ZACCU_PB_SPACES[i]$ZACCU_PB_WORDS[i]"
        done
        buf+="$ZACCU_PB_SPACES[i]"

        _zacculist_translate_color_marks "$buf"
        disp_list2[ZACCULIST_CURRENT_PAGE_IDX]="$REPLY"
}

#
# Main code
#

# Check if there is proper input
if [ "$#" -lt 1 ]; then
    echo "Usage: zaccu-list element_1 ..."
    return 1
fi

REPLY="-1"
typeset -ga reply
reply=()

local wname_main="$1"
integer main_height="$2"
integer main_width="$3"
local wname_status="$4"
integer status_height="$5"
integer status_width="$6"
integer page_height=main_height-2
integer page_width=main_width-2
local header="$7"
local mark_header="$8"

shift 8

typeset -a list disp_list disp_list2
integer last_element=$#
local action
local final_key
integer selection
integer last_element_difference=0
integer current_difference=0
local prev_search_buffer=""
integer prev_uniq_mode=0
integer prev_start_idx=-1
local MBEGIN MEND MATCH mbegin mend match
integer nseg
typeset -A "cursor_state"
cursor_state=( "main" 1 "status" 1 )

# Load configuration
local colorpair border bold
zstyle -s ":accumulator" colorpair colorpair
zstyle -s ":accumulator" border border
zstyle -s ":accumulator" bold bold
[ "$colorpair" = "" ] && colorpair="white/black"
[ "$border" = "0" ] || border="1"
local background="${colorpair#*/}"
local backuptheme="$colorpair/$bold"
zcurses bg "$wname_main" "$colorpair"
zcurses bg "$wname_status" "$colorpair"

[[ "$bold" = "1" || "$bold" = "+bold" ]] && bold="+bold" || bold="-bold"

# Ability to remember the list between calls
if [[ -z "$ZACCULIST_REMEMBER_STATE" || "$ZACCULIST_REMEMBER_STATE" -eq 0 || "$ZACCULIST_REMEMBER_STATE" -eq 2 ]]; then
    ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN=1
    ZACCULIST_CURRENT_IDX=1
    ZACCULIST_IS_SEARCH_MODE=0
    ZACCULIST_SEARCH_BUFFER=""
    ZACCULIST_TEXT_OFFSET=0
    ZACCULIST_IS_UNIQ_MODE=0
    ZACCULIST_CURRENT_SEGMENT=1 # This variable is responsible for horizontal indexing
    typeset -gA ZACCULIST_ACTIVE_SEGMENTS
    ZACCULIST_ACTIVE_SEGMENTS=()

    # Zero - because it isn't known, unless we
    # confirm that first element is selectable
    ZACCULIST_USER_CURRENT_IDX=0
    [[ ${ZACCULIST_NONSELECTABLE_ELEMENTS[(r)1]} != 1 ]] && ZACCULIST_USER_CURRENT_IDX=1
    ZACCULIST_USER_LAST_ELEMENT=$(( last_element - $#ZACCULIST_NONSELECTABLE_ELEMENTS ))

    # 2 is init once, then remember
    [ "$ZACCULIST_REMEMBER_STATE" -eq 2 ] && ZACCULIST_REMEMBER_STATE=1
fi

if [ "$ZACCULIST_START_IN_SEARCH_MODE" -eq 1 ]; then
    ZACCULIST_START_IN_SEARCH_MODE=0
    ZACCULIST_IS_SEARCH_MODE=1
fi

if [ -n "$ZACCULIST_SET_SEARCH_TO" ]; then
    ZACCULIST_SEARCH_BUFFER="$ZACCULIST_SET_SEARCH_TO"
    ZACCULIST_SET_SEARCH_TO=""
fi

if [ "$ZACCULIST_START_IN_UNIQ_MODE" -eq 1 ]; then
    ZACCULIST_START_IN_UNIQ_MODE=0
    ZACCULIST_IS_UNIQ_MODE=1
fi

#
# Listening for input
#

local key keypad

# Clear input buffer
zcurses timeout main 0
zcurses input main key keypad
zcurses timeout main -1
key=""
keypad=""

# This loop makes script faster on some Zsh's (e.g. 5.0.8)
repeat 1; do
    list=( "$@" )
done

last_element="$#list"

while (( 1 )); do
    # Do searching (filtering with string)
    if [ -n "$ZACCULIST_SEARCH_BUFFER" ]; then
        # Compute new list?
        if [[ "$ZACCULIST_SEARCH_BUFFER" != "$prev_search_buffer" || "$ZACCULIST_IS_UNIQ_MODE" -ne "$prev_uniq_mode" ]]; then
            prev_search_buffer="$ZACCULIST_SEARCH_BUFFER"
            prev_uniq_mode="$ZACCULIST_IS_UNIQ_MODE"
            # regenerating list -> regenerating disp_list
            prev_start_idx=-1

            # Take all elements, including duplicates and non-selectables
            typeset +U list
            repeat 1; do
                list=( "$@" )
            done

            # Remove non-selectable elements
            [ "$#ZACCULIST_NONSELECTABLE_ELEMENTS" -gt 0 ] && for i in "${(nO)ZACCULIST_NONSELECTABLE_ELEMENTS[@]}"; do
                list[$i]=()
            done

            # Remove duplicates
            [ "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ] && typeset -U list

            last_element="$#list"

            # Next do the filtering
            local search_buffer="${ZACCULIST_SEARCH_BUFFER%% ##}"
            search_buffer="${search_buffer## ##}"
            search_buffer="${search_buffer//(#m)[][*?|#~^()><\\]/\\$MATCH}"
            local search_pattern=""
            local colsearch_pattern=""
            if [ -n "$search_buffer" ]; then
                # The base bit of this pattern is ((#s)[^$'\1']#THEWORD*|*$'\2'[^$'\1']#THEWORD*) - the word
                # occuring not inside hyper link
                # Pattern will be ((#s)[^$'\1']#FOO*|*$'\2'[^$'\1']#FOO*)~^((#s)[^$'\1']#BAR*|*$'\2'[^$'\1']#BAR*...)
                # ~^ means: "not those that have not this"
                search_pattern=${search_buffer//(#b)([^ ]##)/((#s)[^$'\1']#$match[1]*|*$'\2'[^$'\1']#$match[1]*)~^}
                search_pattern="${search_pattern// ##/}"
                search_pattern="${search_pattern%\~\^}"
                # Pattern will be (foo|bar)
                colsearch_pattern="${search_buffer// ##/|}"

                # The repeat will make the matching work on a fresh heap
                repeat 1; do
                    list=( "${(@M)list:#(#i)$~search_pattern}" )
                done

                last_element="$#list"
            fi

            # Called after processing list
            _zacculist_verify_vars
        fi

        _zacculist_setup_user_vars 1
        typeset -a tmp
        tmp=( "${(z)list[ZACCULIST_CURRENT_IDX]}" )
        nseg="$#tmp"

        integer end_idx=$(( ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN + page_height - 1 ))
        [ "$end_idx" -gt "$last_element" ] && end_idx=last_element

        if [ "$prev_start_idx" -ne "$ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN" ]; then
            prev_start_idx="$ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN"
            disp_list=( "${(@)list[ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN, end_idx]}" )

            if [ -n "$colsearch_pattern" ]; then
                local col=$'\5' reset=$'\25'
                # The repeat will make the matching work on a fresh heap
                repeat 1; do
                    disp_list=( ${(@)disp_list//(#mi)($~colsearch_pattern)/$col${MATCH}$reset} )
                    # [Mark][Text][Red-Mark][Text2][Close]... -> [Mark][Text][Close][Red-Mark][Text2][Close][Mark]
                    # I.e. provide [Close] before [Red-Mark] and [Mark] after red's [Close]
                    while (( 1 )); do
                        match=()
                        disp_list=( ${(@)disp_list//(#b)([$'\3'$'\4'$'\5'$'\6'])([^$'\25']#)$'\5'([^$'\25']#)$'\25'/$match[1]$match[2]$'\25'$'\5'$match[3]$'\25'$match[1]} )
                        [ -z "${match[1]}" ] && break
                    done
                done
            fi
            # We translate color marks into color codes. list still contains marks,
            # and _zacculist_mark_current_segment_in_current_element will be able
            # to restore current element into marks, to switch colors of active
            # segment
            _zacculist_translate_color_marks_in_disp_list
        fi

        # We need second disp_list to be able to disable colorifying for particular elements
        disp_list2=( "${(@)disp_list}" )

        # We have display list, lets replace newlines with ";"
        # This will also replace newlines from within strings,
        # which is an error, but it will be fine when element
        # will be active
        disp_list2=( "${(@)disp_list2//$'\n'/;}" )

        [ "$#disp_list2" -gt 0 ] && _zacculist_mark_current_segment_in_current_element

        # Output colored list
        zaccu-list-draw "$(( (ZACCULIST_CURRENT_IDX-1) % page_height + 1 ))" \
            "$page_height" "$page_width" 1 2 "$ZACCULIST_TEXT_OFFSET" "$wname_main" \
            "$disp_list2[@]"
    else
        # There is no search, but there was in previous loop
        # OR
        # Uniq mode was entered or left out
        # -> compute new list
        if [[ -n "$prev_search_buffer" || "$ZACCULIST_IS_UNIQ_MODE" -ne "$prev_uniq_mode" ]]; then
            prev_search_buffer=""
            prev_uniq_mode="$ZACCULIST_IS_UNIQ_MODE"
            # regenerating list -> regenerating disp_list
            prev_start_idx=-1

            # Take all elements, including duplicates and non-selectables
            typeset +U list
            repeat 1; do
                list=( "$@" )
            done

            # Remove non-selectable elements only when in uniq mode
            [ "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ] && [ "$#ZACCULIST_NONSELECTABLE_ELEMENTS" -gt 0 ] &&
            for i in "${(nO)ZACCULIST_NONSELECTABLE_ELEMENTS[@]}"; do
                list[$i]=()
            done

            # Remove duplicates when in uniq mode
            [ "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ] && typeset -U list

            last_element="$#list"
            # Called after processing list
            _zacculist_verify_vars
        fi

        # "1" - shouldn't bother with non-selectables
        _zacculist_setup_user_vars "$ZACCULIST_IS_UNIQ_MODE"
        typeset -a tmp
        tmp=( "${(z)list[ZACCULIST_CURRENT_IDX]}" )
        nseg="$#tmp"

        integer end_idx=$(( ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN + page_height - 1 ))
        [ "$end_idx" -gt "$last_element" ] && end_idx=last_element

        if [ "$prev_start_idx" -ne "$ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN" ]; then
            prev_start_idx="$ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN"
            disp_list=( "${(@)list[ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN, end_idx]}" )

            # We translate color marks into color codes. list still contains marks,
            # and _zacculist_mark_current_segment_in_current_element will be able
            # to restore current element into marks, to switch colors of active
            # segment
            _zacculist_translate_color_marks_in_disp_list

            [ -n "$ZACCULIST_COLORING_PATTERN" ] && _zacculist_colorify_disp_list
        fi

        # We need second disp_list to be able to disable colorifying for particular elements
        disp_list2=( "${(@)disp_list}" )

        # We have display list, lets replace newlines with ";"
        # This will also replace newlines from within strings,
        # which is an error, but it will be fine when element
        # will be active
        disp_list2=( "${(@)disp_list2//$'\n'/;}" )

        [ "$#disp_list2" -gt 0 ] && _zacculist_mark_current_segment_in_current_element

        # Output the list
        zaccu-list-draw "$(( (ZACCULIST_CURRENT_IDX-1) % page_height + 1 ))" \
            "$page_height" "$page_width" 1 2 "$ZACCULIST_TEXT_OFFSET" "$wname_main" \
            "$disp_list2[@]"
    fi

    [[ "$mark_header" = "1" && "$colorpair" = "white/black" ]] && zcurses attr "$wname_main" "$bold" "magenta/$background"
    [ "$border" = "1" ] && zcurses border "$wname_main"
    zcurses move "$wname_main" 0 2
    zcurses string "$wname_main" "$header"
    [[ "$mark_header" = "1" && "$colorpair" = "white/black" ]] && zcurses attr "$wname_main" "$bold" "$colorpair"
    _zacculist_cursor_visibility "main" "0"
    zcurses refresh "$wname_main"

    # Status window is optional (1/2)
    [ -n "$wname_status" ] && {
        local st_path="" st_cmd=""
        if zaccu_stdlib_decode_hyperlink "${tmp[$ZACCULIST_CURRENT_SEGMENT]}"; then
            st_cmd="${(Q)reply[3]}"
            st_path="${(Q)reply[4]}"
        fi

        local status_msg_strlen
        if [ "$ZACCULIST_IS_SEARCH_MODE" = "1" ]; then
            local _txt2=""
            [ "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ] && _txt2="[-UNIQ-] "
            _zacculist_status_msg "${_txt2}Filtering with: ${ZACCULIST_SEARCH_BUFFER// /+}"
        elif [[ ${ZACCULIST_NONSELECTABLE_ELEMENTS[(r)$ZACCULIST_CURRENT_IDX]} != $ZACCULIST_CURRENT_IDX ||
                -n "$ZACCULIST_SEARCH_BUFFER" || "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ]]; then
            local _txt="" _txt2=""
            [ -n "$ZACCULIST_GREP_STRING" ] && _txt=" [$ZACCULIST_GREP_STRING]"
            [ "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ] && _txt2="[-UNIQ-] "
            _zacculist_status_msg "${_txt2}Current #$ZACCULIST_USER_CURRENT_IDX (of #$ZACCULIST_USER_LAST_ELEMENT entries)${_txt}" "${st_cmd}" "${st_path}"
        else
            _zacculist_status_msg ""
        fi

        [ "$border" = "1" ] && zcurses border "$wname_status"
        # status_msg_strlen is set in _zacculist_status_msg()
        zcurses move "$wname_status" 1 $(( status_msg_strlen + 2 ))
        [ "$ZACCULIST_IS_SEARCH_MODE" -ne 1 ] && _zacculist_cursor_visibility "status" "0" || _zacculist_cursor_visibility "status" "1"
        zcurses refresh "$wname_status" 
    }

    # Draw only?
    [ "$ZACCULIST_DRAW_ONLY" = "1" ] && return

    # Wait for input
    zcurses input main key keypad

    # Get the special (i.e. "keypad") key or regular key
    if [ -n "$key" ]; then
        final_key="$key"
    elif [ -n "$keypad" ]; then
        final_key="$keypad"
    else
        [ -n "$wname_status" ] && {
            _zacculist_status_msg "Inproper input detected"
            zcurses refresh "$wname_status"
            sleep 2
        }
    fi

    integer track_cur_idx="$ZACCULIST_CURRENT_IDX"
    zaccu-list-input "$ZACCULIST_CURRENT_IDX" "$ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN" \
                    "$page_height" "$page_width" "$last_element" "$nseg" "$ZACCULIST_TEXT_OFFSET" \
                    "$final_key" "$ZACCULIST_IS_SEARCH_MODE" "$ZACCULIST_SEARCH_BUFFER" \
                    "$ZACCULIST_IS_UNIQ_MODE" "$ZACCULIST_DISABLE_SEARCH"

    selection="$reply[1]"
    action="$reply[2]"
    ZACCULIST_CURRENT_IDX="$reply[3]"
    ZACCULIST_FROM_WHAT_IDX_LIST_IS_SHOWN="$reply[4]"
    ZACCULIST_TEXT_OFFSET="$reply[5]"
    ZACCULIST_IS_SEARCH_MODE="$reply[6]"
    ZACCULIST_SEARCH_BUFFER="$reply[7]"
    ZACCULIST_IS_UNIQ_MODE="$reply[8]"

    if [ "$ZACCULIST_TRACK_SEGMENTS" = "1" ]; then
        # Remember segment change (element change and segment
        # change cannot occur at the same time)
        ZACCULIST_ACTIVE_SEGMENTS[$track_cur_idx]="$ZACCULIST_CURRENT_SEGMENT"

        if [[ "$track_cur_idx" != "$ZACCULIST_CURRENT_IDX" ]]; then
            # Restore segment or set to 1
            if (( ${+ZACCULIST_ACTIVE_SEGMENTS[$ZACCULIST_CURRENT_IDX]} )); then
                ZACCULIST_CURRENT_SEGMENT="${ZACCULIST_ACTIVE_SEGMENTS[$ZACCULIST_CURRENT_IDX]}"
            else
                ZACCULIST_CURRENT_SEGMENT=1
            fi
        fi
    fi

    if [ "$action" = "SELECT" ]; then
        REPLY="$selection"
        reply=( "$list[@]" )
        break
    elif [[ "$action" = F[1-4] ]]; then
        REPLY="$action"
        reply=( "$list[@]" )
        break
    elif [[ "$action" = "BR_MOVE_LEFT" || "$action" = "BR_MOVE_RIGHT" ]]; then
        REPLY="$action"
        reply=( "$list[@]" )
        break
    elif [ "$action" = "QUIT" ]; then
        REPLY=-1
        reply=( "$list[@]" )
        break
    elif [ "$action" = "REDRAW" ]; then
        zcurses clear "$wname_main" redraw
        # Status window is optional (2/2)
        [ -n "$wname_status" ] && zcurses clear "$wname_status" redraw
    fi
done

# vim: set filetype=zsh:
